#+begin_src clojurescript :tangle ../../src/client/state.cljs
  (ns client.state
    (:require [reagent.core :as r]
              [clojure.string :as str]
              [cljs.spec.alpha :as s]
              ["@element-hq/web-shared-components" :as element-ui])
    (:require-macros [macros :refer [ocall oget]]))
#+end_src

#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defprotocol IMatrixClient
  "Structured access to the Matrix Rust SDK Client"
  (get-sync-service [this])
  (get-rooms [this])
  (get-user-id [this])
  (is-alive? [this]))
#+end_src


#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defprotocol IDisposable
  "Lifecycle management for WASM memory"
  (dispose! [this]))
#+end_src


#+begin_src clojurescript :tangle ../../src/client/state.cljs
(deftype MatrixClient [raw-client]
  IMatrixClient
  (get-sync-service [_] (ocall raw-client :syncService))
  (get-rooms [_] (ocall raw-client :rooms))
  (get-user-id [_] (ocall raw-client :userId))
  (is-alive? [_] (not (nil? raw-client)))

  IDisposable
  (dispose! [_]
    (try
      (ocall raw-client :uniffiDestroy)
      (js/console.log "WASM Client Destroyed")
      (catch js/Error _ nil)))

  Object
  (toString [_] (str "#<MatrixClient " (try (ocall raw-client :userId) (catch js/Error _ "unknown")) ">")))
#+end_src


#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defrecord SDKWorld [client rooms active-room ctrl handles loading?])
#+end_src

;; Enforcement: ensure :client is a MatrixClient (satisfies the protocol)
#+begin_src clojurescript :tangle ../../src/client/state.cljs
(s/def ::matrix-client #(satisfies? IMatrixClient %))
(s/def ::sdk-world (s/keys :req-un [::matrix-client]))
#+end_src

#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defn make-world 
  "Factory to ensure we never inject raw WASM into the state."
  [params]
  (let [new-world (map->SDKWorld params)]
    (if (s/valid? ::sdk-world new-world)
      new-world
      (throw (js/Error. (str "Invalid SDK World State: " (s/explain-str ::sdk-world new-world)))))))
#+end_src

#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defonce sdk-world 
  (r/atom (map->SDKWorld 
            {:client nil 
             :rooms [] 
             :active-room nil 
             :ctrl nil 
             :handles {:entries nil :loading nil}
             :loading? true})))
#+end_src


#+begin_src clojurescript :tangle ../../src/client/state.cljs
(add-watch sdk-world :type-checker
           (fn [_ _ _ new-state]
             (let [c (:client new-state)]
               (when (and c (not (satisfies? IMatrixClient c)))
                 (js/console.error "TYPE VIOLATION: Raw WASM object detected in sdk-world! 
                           Did you forget to wrap it in (MatrixClient. obj)?")))))


#+end_src

             
#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defn hook-vm! 
  "Connects an npm ViewModel to our Reagent world."
  [vm atom-path]
  (let [sync-fn #(swap! sdk-world assoc-in atom-path (.getSnapshot vm))
        _ (sync-fn)
        unsub (.subscribe vm sync-fn)]
    (fn [] 
      (unsub)
      (.dispose vm))))
#+end_src

#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defn mount-vm!
  "Plugs an SDK ViewModel into our global brain."
  [id raw-vm]
  (let [
        update-snap! (fn []
                       (let [snap (.getSnapshot raw-vm)
                             clj-data (js->clj snap :keywordize-keys true)]
                         (swap! sdk-world assoc-in [:snapshots id] clj-data)))
        unsub (.subscribe raw-vm update-snap!)]
    (update-snap!)
    (swap! sdk-world assoc-in [:vms id]
           {:instance raw-vm
            :unsub unsub
            :dispose (fn []
                       (unsub)
                       (.dispose raw-vm))})
    (js/console.log (str "Mounted VM: " id))))
#+end_src

#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defn unmount-vm! 
  "Safely unplugs a ViewModel and clears its memory."
  [id]
  (when-let [vm-map (get-in @sdk-world [:vms id])]
    ((:dispose vm-map))
    (swap! sdk-world update :vms dissoc id)
    (swap! sdk-world update :snapshots dissoc id)
    (js/console.log (str "Unmounted VM: " id))))
#+end_src


#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defn ^:export debug-world []
    (let [data (clj->js @sdk-world)]
      (js/console.log "SDK WORLD DUMP:" data)
      data))
#+end_src

Allow actually setting the client object inside the sdk-world
#+begin_src clojurescript :tangle ../../src/client/state.cljs
(defn set-client! [raw-wasm-obj]
  (when-let [old-client (:client @sdk-world)]
    (dispose! old-client))
  (swap! sdk-world assoc :client (MatrixClient. raw-wasm-obj)))

(set! (.-dump js/window) debug-world)
#+end_src
